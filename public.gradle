if (project.hasProperty("binom.repo.url")) {
    def repoUser = project.hasProperty("binom.repo.user") ? project.property("binom.repo.user") : null
    def repoPassword = project.hasProperty("binom.repo.password") ? project.property("binom.repo.password") : null
    def repoUrl = project.property("binom.repo.url")

    publishing {
        repositories {
            maven {
                name = "BinomRepository"

                url repoUrl

                if (repoUser != null && repoPassword != null) {
                    credentials {
                        username = repoUser
                        password = repoPassword
                    }
                }
            }
        }

        publications.all {
            def repoName = ""
            if (it.name == "kotlinMultiplatform") {
                repoName = project.name
            } else {
                repoName = "${project.name}-${it.name}"
            }
            it.groupId = project.group
            it.artifactId = repoName
            it.version = project.version

            pom {
                scm {
                    connection = "https://github.com/caffeine-mgn/pw.binom.io.git"
                    url = "https://github.com/caffeine-mgn/pw.binom.io"
                }
                developers {
                    developer {
                        id = 'subochev'
                        name = 'Anton Subochev'
                        email = 'caffeine.mgn@gmail.com'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }

}